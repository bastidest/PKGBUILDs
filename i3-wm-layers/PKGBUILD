# Maintainer: bastidest <bastidest at mailbox dot org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

_name=i3-wm
pkgname=$_name-layers
pkgver=4.18.r410.gb62dbcaa
pkgrel=1
pkgdesc='Improved dynamic tiling window manager - layers fork'
arch=('x86_64')
url=https://github.com/bastidest/i3
license=('BSD')
groups=('i3')
depends=('libev' 'libxkbcommon-x11' 'pango' 'startup-notification'
         'xcb-util-cursor' 'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-xrm'
         'yajl')
makedepends=('meson' 'xmlto')
optdepends=('dmenu: for the default program launcher'
            'rofi: for a modern dmenu replacement'
            'i3lock: for the default screen locker'
            'xss-lock: for the default screen locker'
            'i3status: for the default status bar generator'
            'perl: for i3-save-tree and i3-dmenu-desktop'
            'perl-anyevent-i3: for i3-save-tree'
            'perl-json-xs: for i3-save-tree')
replaces=('i3' 'i3bar')
provides=($_name)
backup=('etc/i3/config')
source=("$_name::git+$url#branch=feature/layers")
# source=('git+file:///home/sh/projects/i3#branch=feature/layers')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_name"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/$_name"
  arch-meson build
  ninja -C build
}

package() {
  cd "${srcdir}/$_name"
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
